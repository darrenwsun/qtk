feature Insert data into a segmented table.

  before each
    .qtk.import.loadModule["tbl";`qtk];
    .qutils.hdbRoot:"/tmp/hdb_insert_segmented";
    .qutils.segment1:"/tmp/hdb_insert_segment1";
    .qutils.segment2:"/tmp/hdb_insert_segment2";
    .Q.dd[hsym `$.qutils.hdbRoot; `par.txt] 0: (.qutils.segment1; .qutils.segment2);
    .qutils.tabRef:(hsym `$.qutils.hdbRoot; `date; `SegmentedTable);
    .qtk.tbl.create[.qutils.tabRef; ([] date:2022.01.01 2022.01.02; c1:("aa";"cc"); c2:`a`c)];
    system "l ",.qutils.hdbRoot;

  after each
    .qtk.tbl.drop .qutils.tabRef;
    .qtk.os.rmtree each (.qutils.hdbRoot; .qutils.segment1; .qutils.segment2);

  should insert full data into a segmented table.
    expect given data inserted into the table
      .qu.result:.qtk.tbl.insert[`SegmentedTable; ([] date:2022.01.01 2022.01.02; c1:("bb";"dd"); c2:`b`d)];
      .qtk.db.reload[];
      .qu.compare[
        select from SegmentedTable;
        .Q.en[`:.; ] ([] date:2022.01.01 2022.01.01 2022.01.02 2022.01.02; c1:("aa";"bb";"cc";"dd"); c2:`a`b`c`d)]

  should insert full data into a segmented table, when the table is specified by partitioned table ID.
    expect given data inserted into the table
      .qu.result:.qtk.tbl.insert[.qutils.tabRef; ([] date:2022.01.01 2022.01.02; c1:("bb";"dd"); c2:`b`d)];
      .qtk.db.reload[];
      .qu.compare[
        select from SegmentedTable;
        .Q.en[`:.; ] ([] date:2022.01.01 2022.01.01 2022.01.02 2022.01.02; c1:("aa";"bb";"cc";"dd"); c2:`a`b`c`d)]

  should insert partial data into a segmented table.
    expect given data inserted into the table
      .qu.result:.qtk.tbl.insert[`SegmentedTable; ([] date:2022.01.01 2022.01.02; c2:`b`d)];
      .qtk.db.reload[];
      .qu.compare[
        select from SegmentedTable;
        .Q.en[`:.; ] ([] date:2022.01.01 2022.01.01 2022.01.02 2022.01.02; c1:("aa";"";"cc";""); c2:`a`b`c`d)]
