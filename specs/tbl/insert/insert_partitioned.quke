feature Insert data into a partitioned table.

  before each
    .qtk.import.loadModule["tbl";`qtk];
    .qtk.test.dbDir:"/tmp/hdb_insert_partitioned";
    .qtk.test.tabRef:(hsym `$.qtk.test.dbDir; `date; `PartitionedTable);
    .qtk.tbl.create[.qtk.test.tabRef; ([] date:2022.01.01 2022.01.02; c1:("aa";"cc"); c2:`a`c)];
    system "l ",.qtk.test.dbDir;

  after each
    .qtk.tbl.drop .qtk.test.tabRef;
    .qtk.os.rmtree .qtk.test.dbDir;

  should insert full data into a partitioned table.
    expect given data inserted into the table
      .qu.result:.qtk.tbl.insert[`PartitionedTable; ([] date:2022.01.01 2022.01.02; c1:("bb";"dd"); c2:`b`d)];
      .qtk.db.reload[];
      .qu.compare[
        select from PartitionedTable;
        .Q.en[`:.; ] ([] date:2022.01.01 2022.01.01 2022.01.02 2022.01.02; c1:("aa";"bb";"cc";"dd"); c2:`a`b`c`d)]

  should insert full data into a partitioned table, when the table is specified by partitioned table ID.
    expect given data inserted into the table
      .qu.result:.qtk.tbl.insert[.qtk.test.tabRef; ([] date:2022.01.01 2022.01.02; c1:("bb";"dd"); c2:`b`d)];
      .qtk.db.reload[];
      .qu.compare[
        select from PartitionedTable;
        .Q.en[`:.; ] ([] date:2022.01.01 2022.01.01 2022.01.02 2022.01.02; c1:("aa";"bb";"cc";"dd"); c2:`a`b`c`d)]

  should insert full data into new partitions of a partitioned table.
    expect given data inserted into the table
      .qu.result:.qtk.tbl.insert[`PartitionedTable; ([] date:2022.01.03 2022.01.04; c1:("bb";"dd"); c2:`b`d)];
      .qtk.db.reload[];
      .qu.compare[
        select from PartitionedTable;
        .Q.en[`:.; ] ([] date:2022.01.01 2022.01.02 2022.01.03 2022.01.04; c1:("aa";"cc";"bb";"dd"); c2:`a`c`b`d)]

  should insert partial data into a partitioned table.
    expect given data inserted into the table
      .qu.result:.qtk.tbl.insert[`PartitionedTable; ([] date:2022.01.01 2022.01.02; c2:`b`d)];
      .qtk.db.reload[];
      .qu.compare[
        select from PartitionedTable;
        .Q.en[`:.; ] ([] date:2022.01.01 2022.01.01 2022.01.02 2022.01.02; c1:("aa";"";"cc";""); c2:`a`b`c`d)]
