feature Delete rows of a splayed table given certain criteria.

  before each
    .qtk.import.loadModule["tbl";`qtk];
    .qtk.test.dbDir:"/tmp/hdb_deleteRows_splayed";
    .qtk.test.tabRef:hsym `$.qtk.test.dbDir,"/SplayedTable/";
    .qtk.tbl.create[.qtk.test.tabRef; ([] c1:`a`b`c)];
    system "l ",.qtk.test.dbDir;

  after each
    .qtk.tbl.drop .qtk.test.tabRef;
    .qtk.os.rmtree .qtk.test.dbDir;

  should delete all rows if the criteria is empty.
    expect all rows deleted.
      .qtk.tbl.deleteRows[`SplayedTable; ()];
      .qu.compare[select from SplayedTable; .Q.en[`:.;] ([] c1:`$())]

  should delete all rows if the criteria is empty, when the table is specified by hsym.
    expect all rows deleted.
      .qtk.tbl.deleteRows[.qtk.test.tabRef; ()];
      .qu.compare[select from SplayedTable; .Q.en[`:.;] ([] c1:`$())]

  should delete some rows if they match the criteria.
    expect some rows deleted.
      .qtk.tbl.deleteRows[`SplayedTable; enlist (=;`c1;enlist `c)];
      .qu.compare[select from SplayedTable; .Q.en[`:.;] ([] c1:`a`b)]

  should delete no row if none match the criteria.
    expect no row deleted.
      .qtk.tbl.deleteRows[`SplayedTable; enlist (=;`c1;enlist `d)];
      .qu.compare[select from SplayedTable; .Q.en[`:.;] ([] c1:`a`b`c)]
