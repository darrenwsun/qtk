feature Delete rows of a segmented table given certain criteria.

  before each
    .qtk.import.loadModule["tbl";`qtk];
    .qutils.hdbRoot:"/tmp/hdb_deleteRows_segmented";
    .qutils.segment1:"/tmp/hdb_deleteRows_segment1";
    .qutils.segment2:"/tmp/hdb_deleteRows_segment2";
    .Q.dd[hsym `$.qutils.hdbRoot; `par.txt] 0: (.qutils.segment1; .qutils.segment2);
    .qutils.tabRef:(hsym `$.qutils.hdbRoot; `date; `SegmentedTable);
    .qtk.tbl.create[.qutils.tabRef; ([] date:2022.01.01 2022.01.02 2022.01.02; c1:`a`b`c)];
    system "l ",.qutils.hdbRoot;

  after each
    .qtk.tbl.drop .qutils.tabRef;
    .qtk.os.rmtree .qutils.hdbRoot;

  should delete all rows if the criteria is empty.
    expect all rows deleted.
      .qtk.tbl.deleteRows[`SegmentedTable; ()];
      .qu.compare[select from SegmentedTable; .Q.en[`:.;] ([] date:`date$(); c1:`$())]

  should delete all rows if the criteria is empty, when the table is specified by partitioned table ID.
    expect all rows deleted.
      .qtk.tbl.deleteRows[.qutils.tabRef; ()];
      .qu.compare[select from SegmentedTable; .Q.en[`:.;] ([] date:`date$(); c1:`$())]

  should delete some rows if they match the criteria, for the case that partition key is not used in the criteria.
    expect some rows deleted.
      .qtk.tbl.deleteRows[`SegmentedTable; enlist (=;`c1;enlist`c)];
      .qu.compare[select from SegmentedTable; .Q.en[`:.;] ([] date:2022.01.01 2022.01.02; c1:`a`b)]

  should delete some rows if they match the criteria, for the case that partition key is used in first criterion.
    expect some rows deleted.
      .qtk.tbl.deleteRows[`SegmentedTable; ((=;`date;2022.01.02); (=;`c1;enlist`c))];
      .qu.compare[select from SegmentedTable; .Q.en[`:.;] ([] date:2022.01.01 2022.01.02; c1:`a`b)]

  should delete no row if none match the criteria.
    expect no row deleted.
      .qtk.tbl.deleteRows[`SegmentedTable; enlist (=;`c1;enlist`d)];
      .qu.compare[select from SegmentedTable; .Q.en[`:.;] ([] date:2022.01.01 2022.01.02 2022.01.02; c1:`a`b`c)]
