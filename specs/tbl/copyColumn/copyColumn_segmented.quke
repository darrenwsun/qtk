feature Copy an existing column to a new column for a segmented table.

  before each
    .qtk.import.loadModule["tbl";`qtk];
    .qtk.test.dbDir:"/tmp/hdb_copyColumn_segmented";
    .qtk.test.seg1:"/tmp/hdb_copyColumn_segment1";
    .qtk.test.seg2:"/tmp/hdb_copyColumn_segment2";
    .Q.dd[hsym `$.qtk.test.dbDir; `par.txt] 0: (.qtk.test.seg1; .qtk.test.seg2);
    .qtk.test.tabRef:(hsym `$.qtk.test.dbDir; `date; `SegmentedTable);
    .qtk.tbl.create[.qtk.test.tabRef; ([] date:2022.01.01 2022.01.02 2022.01.02; c1:("aa";"bb";"cc"); c2:`a`b`c)];
    system "l ",.qtk.test.dbDir;

  after each
    .qtk.tbl.drop `SegmentedTable;
    .qtk.os.rmtree each (.qtk.test.dbDir; .qtk.test.seg1; .qtk.test.seg2);

  should copy a column to a new column for a segmented table.
    expect the new column have the same value as the specified column.
      .qtk.tbl.copyColumn[`SegmentedTable;`c2;`c3];
      .qu.compare[exec c3 from select c3 from SegmentedTable; exec c2 from select c2 from SegmentedTable]

  should copy a column to a new column for a segmented table, when the table is specified by partitioned table ID.
    expect the new column have the same value as the specified column.
      .qtk.tbl.copyColumn[.qtk.test.tabRef;`c2;`c3];
      .qu.compare[exec c3 from select c3 from SegmentedTable; exec c2 from select c2 from SegmentedTable]
