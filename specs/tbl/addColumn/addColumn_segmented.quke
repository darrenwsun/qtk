feature Add a column to a segmented table.

  before each
    .qtk.import.loadModule["tbl";`qtk];
    .qtk.test.dbDir:"/tmp/hdb_addColumn_segmented";
    .qtk.test.seg1:"/tmp/hdb_addColumn_segment1";
    .qtk.test.seg2:"/tmp/hdb_addColumn_segment2";
    .Q.dd[hsym `$.qtk.test.dbDir; `par.txt] 0: (.qtk.test.seg1; .qtk.test.seg2);
    .qtk.test.tabRef:(hsym `$.qtk.test.dbDir; `date; `SegmentedTable);
    .qtk.tbl.create[.qtk.test.tabRef; ([] date:2022.01.01 2022.01.02; c1:("aa";"bb"))];
    system "l ",.qtk.test.dbDir;

  after each
    .qtk.tbl.drop `SegmentedTable;
    .qtk.os.rmtree each (.qtk.test.dbDir; .qtk.test.seg1; .qtk.test.seg2);

  should add a column to a segmented table specified by table name.
    expect the column added to the table.
      .qtk.tbl.addColumn[`SegmentedTable;`c2;`test];
      .qu.compare[cols SegmentedTable; `date`c1`c2]

  should add a column to a segmented table specified by partitioned table ID.
    expect the column added to the table.
      .qtk.tbl.addColumn[.qtk.test.tabRef; `c2; `test];
      .qu.compare[cols SegmentedTable; `date`c1`c2]
