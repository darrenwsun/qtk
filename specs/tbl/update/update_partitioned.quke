feature Update values in certain columns of a partitioned table.

  before each
    .qtk.import.loadModule["tbl";`qtk];
    .qutils.hdbRoot:"/tmp/hdb_update_partitioned";
    .qutils.tabRef:(hsym `$.qutils.hdbRoot; `date; `PartitionedTable);
    (hsym `$.qutils.hdbRoot,"/2022.01.01/PartitionedTable/") set ([] c1:1 2 3 4 5);
    (hsym `$.qutils.hdbRoot,"/2022.01.02/PartitionedTable/") set ([] c1:1 2 3 4 5);
    system "l ",.qutils.hdbRoot;

  after each
    .qtk.tbl.drop `PartitionedTable;
    .qtk.os.rmtree .qutils.hdbRoot;

  should be able to change data type of a column.
    expect data type of the column is updated.
      .qtk.tbl.update[`PartitionedTable; (); (enlist`c1)!(enlist enlist`a)];
      .qu.compare[select c1 from PartitionedTable; ([] c1:`sym$10#`a)]

  should be able to change data type of a column, when the table is specified by partitioned table ID.
    expect data type of the column is updated.
      .qtk.tbl.update[(`:.; `date; `PartitionedTable); (); (enlist`c1)!(enlist enlist`a)];
      .qu.compare[select c1 from PartitionedTable; ([] c1:`sym$10#`a)]

  should be able to change certain indices of a column.
    expect certain indices of the column is updated.
      .qtk.tbl.update[`PartitionedTable; enlist (>;`c1;3); (enlist`c1)!(enlist (*;`c1;2))];
      .qu.compare[select c1 from PartitionedTable; ([] c1:1 2 3 8 10 1 2 3 8 10)]

  should be able to change certain indices of a column.
    expect certain indices of the column is updated.
      .qtk.tbl.update[`PartitionedTable; ((=;`date;2022.01.01); (>;`c1;3)); (enlist`c1)!(enlist (*;`c1;2))];
      .qu.compare[select c1 from PartitionedTable; ([] c1:1 2 3 8 10 1 2 3 4 5)]

  should raise error "type" if it's a partial update and the new values don't have the same type as other values.
    expect error "type"
      .qu.compare[
        .[.qtk.tbl.update; (`PartitionedTable; enlist (>;`c1;3); (enlist`c1)!(enlist enlist `a)); {x}];
        "type"
      ]
