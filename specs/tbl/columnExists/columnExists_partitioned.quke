feature Check if a column exists in a partitioned table.

  before each
    .qtk.import.loadModule["tbl";`qtk];
    .qtk.test.dbDir:"/tmp/hdb_columnExists_partitioned";
    .qtk.test.tabRef:(hsym `$.qtk.test.dbDir; `date; `PartitionedTable);
    .qtk.tbl.create[.qtk.test.tabRef; ([] date:2022.01.01 2022.01.02; c1:1 2; c2:`a`b)];
    system "l ",.qtk.test.dbDir;

  after each
    .qtk.tbl.drop .qtk.test.tabRef;
    .qtk.os.rmtree .qtk.test.dbDir;

  should return `1b` if a column exists.
    expect `1b`.
      .qtk.tbl.columnExists[`PartitionedTable; `c1]

  should return `0b` if a column doesn't exist at all.
    expect `0b`.
      not .qtk.tbl.columnExists[`PartitionedTable; `c3]

  should return `1b` if a column exists, when the table is specified by partitioned table ID.
    expect `1b`.
      .qtk.tbl.columnExists[.qtk.test.tabRef; `c1]

  should return `0b` if a column doesn't exist in `.d` file in the latest partition.
    expect `0b`.
      tablePath:.Q.par[`:.; 2022.01.02; `PartitionedTable];
      allColumns:.qtk.db._getColumns tablePath;
      @[tablePath; `.d; :; allColumns except `c1];
      .qtk.db.reload[];
      not .qtk.tbl.columnExists[`PartitionedTable; `c1]

  should return `0b` if a column doesn't exist in the filesystem in the latest partition.
    expect `0b`.
      columnPath:.Q.dd[.Q.par[`:.; 2022.01.02; `PartitionedTable]; `c1];
      .qtk.tbl._deleteColumnData columnPath;
      .qtk.db.reload[];
      not .qtk.tbl.columnExists[`PartitionedTable; `c1]
