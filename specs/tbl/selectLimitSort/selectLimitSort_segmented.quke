feature Select from a segmented table based on given criteria, groupings, and column mappings, in a similar format to functional select.

  before each
    .qtk.import.loadModule["tbl";`qtk];
    .qtk.test.dbDir:"/tmp/hdb_selectLimitSort_segmented";
    .qtk.test.seg1:"/tmp/hdb_selectLimitSort_segment1";
    .qtk.test.seg2:"/tmp/hdb_selectLimitSort_segment2";
    .Q.dd[hsym `$.qtk.test.dbDir; `par.txt] 0: (.qtk.test.seg1; .qtk.test.seg2);
    .qtk.test.tabRef:(hsym `$.qtk.test.dbDir; `date; `SegmentedTable);
    .qtk.tbl.create[.qtk.test.tabRef; ([] date:2022.01.01 2022.01.01 2022.01.02 2022.01.02; c1:("bb";"aa";"cc";"dd"); c2:`b`a`c`d)];
    system "l ",.qtk.test.dbDir;

  after each
    .qtk.tbl.drop `SegmentedTable;
    .qtk.os.rmtree each (.qtk.test.dbDir; .qtk.test.seg1; .qtk.test.seg2);

  should select from a plain table based on given criteria, groupings, and column mappings.
    expect data sorted by column c2 in ascending order
      .qu.result:.qtk.tbl.selectLimitSort[`SegmentedTable; (); 0b; (); 0W; (<:; `c2)];
      .qu.compare[.qu.result; .Q.en[`:.; ] ([] date:2022.01.01 2022.01.01 2022.01.02 2022.01.02; c1:("aa";"bb";"cc";"dd"); c2:`a`b`c`d)]
    expect data sorted by column c2 in descending order
      .qu.result:.qtk.tbl.selectLimitSort[`SegmentedTable; (); 0b; (); 0W; (>:; `c2)];
      .qu.compare[.qu.result; .Q.en[`:.; ] ([] date:2022.01.02 2022.01.02 2022.01.01 2022.01.01; c1:("dd";"cc";"bb";"aa"); c2:`d`c`b`a)]
