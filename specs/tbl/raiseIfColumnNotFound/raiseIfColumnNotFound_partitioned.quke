feature Check if a column exists in a partitioned table.

  before each
    .qtk.import.loadModule["tbl";`qtk];
    .qtk.test.dbDir:"/tmp/hdb_raiseIfColumnNotFound_partitioned";
    .qtk.test.tabRef:(hsym `$.qtk.test.dbDir; `date; `PartitionedTable);
    .qtk.tbl.create[.qtk.test.tabRef; ([] date:2022.01.01 2022.01.02; c1:1 2; c2:`a`b)];
    system "l ",.qtk.test.dbDir;

  after each
    .qtk.tbl.drop .qtk.test.tabRef;
    .qtk.os.rmtree .qtk.test.dbDir;

  should raise "ColumnNotFoundError" if a column is not found.
    expect error "ColumnNotFoundError: c3".
      .qu.compare[
        .[.qtk.tbl.raiseIfColumnNotFound; (PartitionedTable;`c3); {x}];
        "ColumnNotFoundError: c3"
      ]
    expect error "ColumnNotFoundError: c3" when the table is passed by name.
      .qu.compare[
        .[.qtk.tbl.raiseIfColumnNotFound; (`PartitionedTable;`c3); {x}];
        "ColumnNotFoundError: c3 on PartitionedTable"
      ]
    expect error "ColumnNotFoundError: c3" when the table is passed by partitioned table ID.
      .qu.compare[
        .[.qtk.tbl.raiseIfColumnNotFound; (.qtk.test.tabRef;`c3); {x}];
        "ColumnNotFoundError: c3 on ",string ` sv .qtk.test.tabRef
      ]

  should not raise "ColumnNotFoundError" if a column is found.
    expect no error.
      .qu.compare[
        .qtk.tbl.raiseIfColumnNotFound[PartitionedTable;`c1];
        (::)
      ]
    expect no error.
      .qu.compare[
        .qtk.tbl.raiseIfColumnNotFound[`PartitionedTable;`c1];
        (::)
      ]
    expect no error.
      .qu.compare[
        .qtk.tbl.raiseIfColumnNotFound[.qtk.test.tabRef;`c1];
        (::)
      ]
