feature Get entries at given indices of a partitioned table.

  before each
    .qtk.import.loadModule["tbl";`qtk];
    .qutils.hdbRoot:"/tmp/hdb_index_segmented";
    .qutils.segment1:"/tmp/hdb_index_segment1";
    .qutils.segment2:"/tmp/hdb_index_segment2";
    .Q.dd[hsym `$.qutils.hdbRoot; `par.txt] 0: (.qutils.segment1; .qutils.segment2);
    .qutils.tabRef:(hsym `$.qutils.hdbRoot; `date; `SegmentedTable);
    .qtk.tbl.create[.qutils.tabRef; ([] date:2022.01.01 2022.01.02 2022.01.02; c1:("aa";"bb";"cc"); c2:`a`b`c)];
    system "l ",.qutils.hdbRoot;

  after each
    .qtk.tbl.drop `SegmentedTable;
    .qtk.os.rmtree each (.qutils.hdbRoot; .qutils.segment1; .qutils.segment2);

  should return empty table if the indices are empty or out of bound.
    expect empty table if the indices are empty.
      .qu.compare[
        .qtk.tbl.at[SegmentedTable; `int$()];
        .Q.en[`:.; ] ([] date:`date$(); c1:(); c2:`$())]
    expect empty table if the indices are empty when the table is passed by name.
      .qu.compare[
        .qtk.tbl.at[`SegmentedTable; `int$()];
        .Q.en[`:.; ] ([] date:`date$(); c1:(); c2:`$())]
    expect empty table if the indices are out of bound.
      .qu.compare[
        .qtk.tbl.at[SegmentedTable; 4 5];
        .Q.en[`:.; ] ([] date:`date$(); c1:(); c2:`$())]

  should return entries at the specified indices if the indices are non-empty.
    expect entries at the specified indices if the indices are non-empty.
      .qu.compare[
        .qtk.tbl.at[`SegmentedTable; 0 2];
        .Q.en[`:.; ] ([] date:2022.01.01 2022.01.02; c1:("aa";"cc"); c2:`a`c)]
