feature Check if a column exists in a partitioned table.

  before each
    .qtk.import.loadModule["tbl";`qtk];
    .qutils.hdbRoot:"/tmp/hdb_raiseIfColumnExists_partitioned";
    .qutils.tabRef:(hsym `$.qutils.hdbRoot; `date; `PartitionedTable);
    .qtk.tbl.create[.qutils.tabRef; ([] date:2022.01.01 2022.01.02; c1:1 2; c2:`a`b)];
    system "l ",.qutils.hdbRoot;

  after each
    .qtk.tbl.drop .qutils.tabRef;
    .qtk.os.rmtree .qutils.hdbRoot;

  should raise "ColumnExistsError" if a column exists.
    expect error "ColumnExistsError: c1".
      .qu.compare[
        .[.qtk.tbl.raiseIfColumnExists; (PartitionedTable;`c1); {x}];
        "ColumnExistsError: c1"
      ]
    expect error "ColumnExistsError: c1" when the table is passed by name.
      .qu.compare[
        .[.qtk.tbl.raiseIfColumnExists; (`PartitionedTable;`c1); {x}];
        "ColumnExistsError: c1 on PartitionedTable"
      ]
    expect error "ColumnExistsError: c1" when the table is passed by partitioned table ID.
      .qu.compare[
        .[.qtk.tbl.raiseIfColumnExists; (.qutils.tabRef;`c1); {x}];
        "ColumnExistsError: c1 on ",string ` sv .qutils.tabRef
      ]

  should not raise "ColumnExistsError" if a column doesn't exist.
    expect no error.
      .qu.compare[
        .qtk.tbl.raiseIfColumnExists[PartitionedTable;`c3];
        (::)
      ]
    expect no error.
      .qu.compare[
        .qtk.tbl.raiseIfColumnExists[`PartitionedTable;`c3];
        (::)
      ]
    expect no error.
      .qu.compare[
        .qtk.tbl.raiseIfColumnExists[.qutils.tabRef;`c3];
        (::)
      ]
