feature Check if a column exists in a partitioned table.

  before each
    .qtk.import.loadModule["tbl";`qtk];
    .qtk.test.dbDir:"/tmp/hdb_raiseIfColumnExists_segmented";
    .qtk.test.seg1:"/tmp/hdb_raiseIfColumnExists_segment1";
    .qtk.test.seg2:"/tmp/hdb_raiseIfColumnExists_segment2";
    .Q.dd[hsym `$.qtk.test.dbDir; `par.txt] 0: (.qtk.test.seg1; .qtk.test.seg2);
    .qtk.test.tabRef:(hsym `$.qtk.test.dbDir; `date; `SegmentedTable);
    .qtk.tbl.create[.qtk.test.tabRef; ([] date:2022.01.01 2022.01.02; c1:1 2; c2:`a`b)];
    system "l ",.qtk.test.dbDir;

  after each
    .qtk.tbl.drop `SegmentedTable;
    .qtk.os.rmtree each (.qtk.test.dbDir; .qtk.test.seg1; .qtk.test.seg2);

  should raise "ColumnExistsError" if a column exists.
    expect error "ColumnExistsError: c1".
      .qu.compare[
        .[.qtk.tbl.raiseIfColumnExists; (SegmentedTable;`c1); {x}];
        "ColumnExistsError: c1"
      ]
    expect error "ColumnExistsError: c1" when the table is passed by name.
      .qu.compare[
        .[.qtk.tbl.raiseIfColumnExists; (`SegmentedTable;`c1); {x}];
        "ColumnExistsError: c1 on SegmentedTable"
      ]
    expect error "ColumnExistsError: c1" when the table is passed by partitioned table ID.
      .qu.compare[
        .[.qtk.tbl.raiseIfColumnExists; (.qtk.test.tabRef;`c1); {x}];
        "ColumnExistsError: c1 on ",string ` sv .qtk.test.tabRef
      ]

  should not raise "ColumnExistsError" if a column doesn't exist.
    expect no error.
      .qu.compare[
        .qtk.tbl.raiseIfColumnExists[SegmentedTable;`c3];
        (::)
      ]
    expect no error when the table is passed by name.
      .qu.compare[
        .qtk.tbl.raiseIfColumnExists[`SegmentedTable;`c3];
        (::)
      ]
    expect no error when the table is passed by partitioned table ID.
      .qu.compare[
        .qtk.tbl.raiseIfColumnExists[.qtk.test.tabRef;`c3];
        (::)
      ]
