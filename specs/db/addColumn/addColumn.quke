feature Add a column to a table.

  before each
    import "db";
    .qutils.hdbRoot:"/tmp/hdb_addColumn";
    Table:([] c1:10b; c2:1 2);
    (hsym `$.qutils.hdbRoot,"/2022.01.01/Table/") set Table;
    (hsym `$.qutils.hdbRoot,"/2022.01.02/Table/") set Table;
    system "l ",.qutils.hdbRoot;
    `InMemoryTable set ([] c1:1 2);

  after each
    system "rm -rf ",.qutils.hdbRoot;

  should add a column to an in-memory table with a default value.
    expect the column added to the table.
      .qtk.db.addColumn[`InMemoryTable;`c2;`test];
      .qu.compare[cols InMemoryTable; `c1`c2]

  should add a column to a partitioned table with a default value.
    expect the column added to the table.
      .qtk.db.addColumn[`Table;`c3;`test];
      system "l .";
      .qu.compare[cols Table; `date`c1`c2`c3]

  should add a column to a partitioned table using an existing data file if it's length- and typed-compliant.
    expect the column added to the table.
      tablePaths:.Q.par[`:.; ; `Table] each .qtk.db.getPartitions[];
      .qtk.db._deleteColumnHeader[; `c2] each tablePaths;
      system "l .";

      .qtk.db.addColumn[`Table; `c2; 0];
      system "l .";
      .qu.compare[select c2 from Table; ([] c2:1 2 1 2)]

  should add a column to a partitioned table using default value, if a data file exists but it's not compliant.
    expect the column added to the table.
      tablePaths:.Q.par[`:.; ; `Table] each .qtk.db.getPartitions[];
      .qtk.db._deleteColumnHeader[; `c2] each tablePaths;
      @[; `c2; :; enlist 1] each tablePaths;
      system "l .";

      .qtk.db.addColumn[`Table; `c2; 0];
      system "l .";
      .qu.compare[select c2 from Table; ([] c2:4#0)]

  should raise error "NameError: invalid column name [*]" if the colum name is not valid.
    expect error "NameError: invalid column name [*]" if the colum name is not valid.
      .qu.compare[
        @[.qtk.db.addColumn[`Table; ; 0]; `$"53"; {x}];
        "NameError: invalid column name [53]"]

  should raise error "ColumnExistsError: [*]" if the column exists.
    expect error "ColumnExistsError: [*]" if the column exists.
      .qu.compare[
        @[.qtk.db.addColumn[`Table; ; 0]; `c1; {x}];
        "ColumnExistsError: [c1]"]
