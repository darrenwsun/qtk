feature Add a column to a table.

  before
    import "db";
    .qutils.hdbRoot:"/tmp/hdb_addColumn";
    Table:([] c1:`boolean$(); c2:`long$());
    (hsym `$.qutils.hdbRoot,"/2022.01.01/Table/") set Table;
    (hsym `$.qutils.hdbRoot,"/2022.01.02/Table/") set Table;
    system "l ",.qutils.hdbRoot;
    `InMemoryTable set ([] c1:1 2);

  after
    system "rm -rf ",.qutils.hdbRoot;

  should add a column to an in-memory table with a default value.
    expect the column added to the table.
      .db.addColumn[`InMemoryTable;`c2;`test];
      .qu.compare[cols InMemoryTable; `c1`c2]

  should add a column to a partitioned table with a default value.
    expect the column added to the table.
      .db.addColumn[`Table;`c3;`test];
      system "l .";
      .qu.compare[cols Table; `date`c1`c2`c3]

  should raise error "NameError: invalid column name [*]" if the colum name is not valid.
    expect error "NameError: invalid column name [*]" if the colum name is not valid.
      .qu.compare[
        @[.db.addColumn[`Table; ; 0]; `$"53"; {x}];
        "NameError: invalid column name [53]"]
