feature Add a table.

  before each
    import "db";
    .qutils.hdbRoot:"/tmp/hdb_addTable";
    Table:([] c1:`boolean$(); c2:`long$());
    (hsym `$.qutils.hdbRoot,"/2022.01.01/Table/") set Table;
    (hsym `$.qutils.hdbRoot,"/2022.01.02/Table/") set Table;
    system "l ",.qutils.hdbRoot;

  after each
    system "rm -rf ",.qutils.hdbRoot;

  should add an in-memory table.
    expect the table added.
      .db.addTable[`InMemoryTable; ([] c1:1 0; c2:10b); `Normal];
      .qu.compare[InMemoryTable; ([] c1:1 0; c2:10b)] and .qu.compare[.Q.qp InMemoryTable; 0]

  should add a partitioned table.
    expect the table added.
      .db.addTable[`PartitionedTable; ([] c1:1 0; c2:10b); `Partitioned];
      system "l .";
      .qu.compare[select from PartitionedTable; ([] date:2022.01.01 2022.01.02) cross ([] c1:1 0; c2:10b)]
        and .qu.compare[.Q.qp PartitionedTable; 1b]

  should raise error "RuntimeError: invalid table type [*]" if the table type is not valid.
    expect error "RuntimeError: invalid table type [*]" if the table type is not valid.
      .qu.compare[
        @[.db.addTable[`PartitionedTable; ([] c1:1 0; c2:10b); ]; `InvalidTableType; {x}];
        "RuntimeError: invalid table type [InvalidTableType]"]
